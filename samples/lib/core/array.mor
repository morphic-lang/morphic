import Iter expose { Iter }

// TODO: This is a temporary workaround.  We should have a namespacing system for builtins.
module NoShadow = {
  pub get_: (Array a, Int) -> a = get
}

pub get: (Array a, Int) -> a = NoShadow.get_

pub set(arr: Array a, i: Int, val: a): Array a =
  let (_, hole) = extract(arr, i) in
  hole(val)

pub update(arr: Array a, i: Int, f: a -> a): Array a =
  let (x, hole) = extract(arr, i) in
  hole(f(x))

concat_from(a: Array t, b: Array t, i: Int): Array t =
  if i = len(b) {
    a
  } else {
    concat_from(push(a, get(b, i)), b, i + 1)
  }

pub concat(a: Array t, b: Array t): Array t =
  concat_from(a, b, 0)

fill_with_rec(arr: Array t, count: Int, mk_val: () -> t): Array t =
  if count > 0 {
    fill_with_rec(push(arr, mk_val()), count - 1, mk_val)
  } else {
    arr
  }

pub fill_with(count: Int, mk_val: () -> t): Array t =
  fill_with_rec(reserve([], count), count, mk_val)

pub fill(count: Int, val: t): Array t =
  fill_with(count, \() -> val)

pub from_iter(iter: Iter a): Array a =
  Iter.foldr(iter, [], \(x, arr) -> push(arr, x))
